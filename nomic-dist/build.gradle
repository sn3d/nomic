apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'distribution'
apply plugin: 'nebula.ospackage'
apply plugin: 'com.jfrog.bintray'

jar {
    manifest {
        attributes 'Main-Class': 'nomic.NomicRunner'
    }
}

distributions {
    main {
        baseName = 'nomic'
        contents {
            from(project(':nomic-app').buildDir.toString() + "/libs") {
                fileMode 0640
                into 'lib'
                include "nomic.jar"
            }

            from("src/bin") {
                fileMode 0740
                into "bin"
            }

            from("src/conf") {
                fileMode 0640
                into "conf"
            }
        }
    }
}

distZip {
    archiveName baseName+'-'+version+'-bin.zip'
}

distTar {
    archiveName baseName+'-'+version+'-bin.tar'
}


ospackage {
    packageName = 'nomic'
    version = project.version
    release = '1'
    os = LINUX

    from("src/bin") {
        fileMode 0740
        into "/usr/share/nomic/bin"
    }

    from(project(':nomic-app').buildDir.toString() + "/libs") {
        fileMode 0640
        include "nomic.jar"
        into "/usr/share/nomic/lib"
    }

    from("src/conf") {
        fileType CONFIG | NOREPLACE
        fileMode 0640
        into "/etc/nomic"
        permissionGroup 'hdfs'
        user 'hdfs'
    }

    link("/usr/share/nomic/conf", "/etc/nomic")
    link("/usr/bin/nomic", "/usr/share/nomic/bin/nomic")
    link("/usr/bin/nomicDebug", "/usr/share/nomic/bin/nomicDebug")
}

assemble.dependsOn buildRpm, buildDeb, distZip, distTar
installDist.dependsOn shadowJar


bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayKey') ? project.property('bintrayKey') : System.getenv('BINTRAY_KEY')
    filesSpec {
        from 'build/distributions'
        into '.'
    }
    pkg {
        repo = 'nomic-repo'
        name = 'nomic-app'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/sn3d/nomic.git'
        websiteUrl = "https://github.com/sn3d/"
        issueTrackerUrl = 'https://github.com/sn3d/nomic/issues'
        version {
            name = project.version
            released = new Date()
        }
    }
}
